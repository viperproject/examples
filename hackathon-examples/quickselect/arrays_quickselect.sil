/* https://en.wikipedia.org/wiki/Quickselect
 *
 * Changes w.r.t. the wikipedia code:
 *   - Select returns the index of the n-th smallest element, not the element
 *     itself
 *   - In order to be able to specify that an array has been permuted by
 *     certain operations, the permutation witness (pw) is made explicit
 *     (Leino, Monahan - Dafny meets the Verification Benchmarks Challenge)
 */

method swap(a: IArray, i: Int, j: Int)
  requires 0 <= i && i < len(a)
  requires 0 <= j && j < len(a)
  requires acc(loc(a, i).val)
  requires i != j ==> acc(loc(a, j).val)
  ensures acc(loc(a, i).val)
  ensures i != j ==> acc(loc(a, j).val)
  ensures loc(a, i).val == old(loc(a, j).val)
  ensures loc(a, j).val == old(loc(a, i).val)
{
  var t: Int := loc(a, i).val
  loc(a, i).val := loc(a, j).val
  loc(a, j).val := t
}

/* permuted(a, left, pw) says that the element that is now at a[i] was originally
 * at a[pw[i]] (modulo some index shifting because pw only covers the segment
 * a[left..right]).
 *
 * Technical detail: This axiom could yield a matching loop if a substitution
 * such as j := left + i, ~~> i == j - left, would be made. In this case,
 * loc(a, j) could be chosen as a trigger, and an instantiation with loc(a, N)
 * would yield the term loc(a, pw[N - left]) (ignoring old), which can be used
 * to trigger the axiom again, causing a matching loop.
 */
define permuted(a, left, pw)
  forall i: Int :: 0 <= i && i < |pw| ==> loc(a, left + i).val == old(loc(a, pw[i]).val)
    /* {pw[i]} would be a well-behaving trigger */

method partition(a: IArray, left: Int, right: Int, pivotIndex: Int) returns (storeIndex: Int, pw: Seq[Int])
  /* All indices are valid and their relative order is satisfied */
  requires 0 <= left && left < right && right < len(a)
  requires left <= pivotIndex && pivotIndex <= right
  /* Permissions to the array segment with indices [left..right] */
  requires forall i: Int :: left <= i && i <= right ==> acc(loc(a, i).val)

  /* Returned index is a valid index */
  ensures left <= storeIndex && storeIndex <= right
  /* Return all taken permissions */
  ensures forall i: Int :: left <= i && i <= right ==> acc(loc(a, i).val)
  /* The pivot element is now stored at a[storeIndex] */
  ensures loc(a, storeIndex).val == old(loc(a, pivotIndex).val)
  /* All elements left (right) of the pivot are smaller than (at least as big as) the pivot */
  ensures forall i: Int :: left <= i && i < storeIndex  ==> loc(a, i).val < loc(a, storeIndex).val
  ensures forall i: Int :: storeIndex < i && i <= right ==> loc(a, storeIndex).val <= loc(a, i).val
  /* pw witnesses a permutation of the array segment */
  ensures |pw| == right + 1 - left
  ensures forall i: Int :: 0 <= i && i < |pw| ==> left <= pw[i] && pw[i] <= right
  ensures forall i: Int, j: Int :: 0 <= i && i < j && j < |pw| ==> pw[i] != pw[j]
  /* The array segment has been permuted */
  ensures permuted(a, left, pw)
{
  var pivotValue: Int := loc(a, pivotIndex).val

  pw := [left..right + 1)

  swap(a, pivotIndex, right) /* Move pivot to end, i.e. it is now at a[right] */

  /* Swap pw[pivotIndex - left] and pw[right - left], which corresponds to
   * swapping a[pivotIndex] and a[right]
   */
  pw := pw[pivotIndex - left := pw[right - left]]
          [right - left := pw[pivotIndex - left]]

  storeIndex := left
  var j: Int := left

  while (j < right)
    /* All indices are valid and their relative order is satisfied */
    invariant left <= j && j <= right
    invariant left <= storeIndex && storeIndex <= j
    /* Permissions to the array segment.
     * NOTE: Permissions to a[left..right) would suffice, and it would preserve
     * information about the location of the pivot element, i.e. it would make
     * invariant INV-PIV unnecessary. The current invariant, however, makes it
     * possible to re-use the permuted(...) assertion.
     */
    invariant forall i: Int :: i in [left..right+1) ==> acc(loc(a, i).val)
    invariant loc(a, right).val == pivotValue /* INV-PIV */
    /* All elements left (right) of the pivot are smaller than (at least as big as) the pivot */
    invariant forall i: Int :: i in [left..storeIndex) ==> loc(a, i).val < pivotValue
    invariant forall i: Int :: i in [storeIndex..j) ==> pivotValue <= loc(a, i).val
    /* pw witnesses a permutation of the already visited array segment */
    invariant |pw| == right + 1 - left
    invariant forall i: Int :: 0 <= i && i < |pw| ==> left <= pw[i] && pw[i] <= right
    invariant forall i: Int, k: Int :: 0 <= i && i < k && k < |pw| ==> pw[i] != pw[k]
    /* The array segment has been permuted */
    invariant permuted(a, left, pw)
  {
    if (loc(a, j).val < pivotValue) {

      swap(a, j, storeIndex)

      /* Swap pw[storeIndex - left] and pw[j - left], which corresponds to
       * swapping a[storeIndex] and a[j]
       */
      pw := pw[storeIndex - left := pw[j - left]]
              [j - left := pw[storeIndex - left]]

      storeIndex := storeIndex + 1
    }

    j := j + 1
  }

  swap(a, right, storeIndex)  /* Move pivot to its final position */
}

method select_rec(a: IArray, left: Int, right: Int, n: Int) returns (idx: Int, pw: Seq[Int])
  /* All indices are valid and their relative order is satisfied */
  requires 0 <= left && left <= right && right < len(a)
  requires left <= n && n <= right
  /* Permissions to the array segment with indices [left..right] */
  requires forall i: Int :: left <= i && i <= right ==> acc(loc(a, i).val)

  /* Returned index is a valid index */
  ensures left <= idx && idx <= right
  /* Return all taken permissions */
  ensures forall i: Int :: left <= i && i <= right ==> acc(loc(a, i).val)
  /* From P1 and P2 it follows that loc(a, idx).val is the n-th smallest element */
  ensures idx == n /* P1 */
  ensures forall i: Int :: left <= i && i < idx  ==> loc(a, i).val   <=  loc(a, idx).val /* P2 */
  ensures forall i: Int :: idx < i && i <= right ==> loc(a, idx).val <= loc(a, i).val /* P3 */
  /* pw witnesses a permutation of the array segment */
  ensures |pw| == right + 1 - left
  ensures forall i: Int :: 0 <= i && i < |pw| ==> left <= pw[i] && pw[i] <= right
  ensures forall i: Int, j: Int :: 0 <= i && i < j && j < |pw| ==> pw[i] != pw[j]
  /* The array segment has been permuted */
  ensures permuted(a, left, pw)
{
  var pwPar: Seq[Int]
  var pwRec: Seq[Int]

  if (left == right) {
// assume false
    /* If the list contains only one element, return it */
    idx := left

    pw := Seq(left)
  } else {
    var pivotIndex: Int
    inhale left <= pivotIndex && pivotIndex <= right /* Non-det. choose a pivot index */

    pivotIndex, pwPar := partition(a, left, right, pivotIndex)

    if (n == pivotIndex) {
// assume false
      idx := n
      pw := pwPar
    } elseif (n < pivotIndex) {
// assume false
      idx, pwRec := select_rec(a, left, pivotIndex - 1, n)

      /* In order to prove P3, we need to know that
       *   loc(a, idx).val <= loc(a, pivotIndex).val
       * This follows from the fact that the array segment a[left..pivotIndex - 1]
       * has been permuted, as witnessed by pw.
       * Concluding this (currently) needs to be triggered explicitly by the
       * following assertion.
       */
      assert dummy(pwRec[idx-left])


      // var pwRight: Seq[Int] := pwPar[pivotIndex-left..]
      // assert forall i: Int, j: Int :: 0 <= i && i < j && j < |pwPar| ==> pwPar[i] != pwPar[j]
      // assert forall i: Int, j: Int :: 0 <= i && i < j && j < |pw| ==> pw[i] != pw[j]
      // assert forall i: Int, j: Int :: 0 <= i && i < j && j < |pwRight| ==> pwRight[i] != pwRight[j]
        /* XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX */
        /* TODO: The forall assumed afterwards includes invalid triggers! */


      /* Recall that pw should witness that the element that is now at a[i] previously
       * was at a[pw[i]] (modulo index shifting).
       *
       * Up to this point
       *   1. the call to partition permuted a[left..right],
       *      witnessed by pwPar
       *   2. the recursive call to select_rec permuted a[left..pivotIndex - 1],
       *      witnessed by pwRec
       *
       * Hence, an element that is now at a[i] previously was at
       *   - a[pwPar[pwRec[i]]]  if left <= i <= pivotIndex - 1
       *   - a[pwPar[i]]         if pivotIndex <= i <= right
       */

      inhale |pw| == |pwPar|
      inhale forall i: Int :: 0 <= i && i < |pwRec| ==> pw[i] == pwPar[pwRec[i] - left]
      inhale forall i: Int :: |pwRec| <= i && i < |pwPar| ==> pw[i] == pwPar[i]
    } else {
// assume false
      idx, pwRec := select_rec(a, pivotIndex + 1, right, n)

      /* Mention pwRec, required to prove P3 (as above) */
      assert dummy(pwRec[idx - (pivotIndex + 1)])

      inhale |pw| == |pwPar|
      inhale forall i: Int :: 0 <= i && i <= pivotIndex - left ==> pw[i] == pwPar[i]
      inhale forall i: Int :: pivotIndex + 1 - left <= i && i < |pw| ==> pw[i] == pwPar[pwRec[i - (pivotIndex + 1 - left)] - left]
    }
  }
}

method client(a: IArray)
  requires 10 < len(a)
  requires forall i: Int :: 0 <= i && i < len(a) ==> acc(loc(a, i).val)
  requires forall i: Int :: 0 <= i && i <= 10 ==> loc(a, i).val == i
  ensures forall i: Int :: 0 <= i && i < len(a) ==> acc(loc(a, i).val)
{
  var idx: Int
  var pw: Seq[Int]

  idx, pw := select_rec(a, 0, 10, 3)

  assert idx == 3
  assert loc(a, idx).val == old(loc(a, pw[idx]).val)

  assert old(loc(a, 0).val) == 0
  assert old(loc(a, 3).val) == 3
  assert old(loc(a, 2).val) != old(loc(a, 3).val)

  /* Cannot been proven (directly, yet) */
  //assert loc(a, idx).val == 3
  //assert loc(a, 2).val != loc(a, 3).val
}


/* Encoding of arrays */

field val: Int

domain IArray {
  function loc(a: IArray, i: Int): Ref
  function len(a: IArray): Int
  function first(r: Ref): IArray
  function second(r: Ref): Int

  axiom all_diff {
    forall a: IArray, i: Int :: {loc(a, i)}
      first(loc(a, i)) == a && second(loc(a, i)) == i
  }

  axiom length_nonneg {
    forall a: IArray :: len(a) >= 0
  }
}

/* Misc */

function dummy(i: Int): Bool { true }
